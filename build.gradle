allprojects {
	group = 'com.dubyatoo'

	repositories {
	    mavenCentral()
	}
}

subprojects {
	buildscript {
		repositories {
			maven {
				url "https://plugins.gradle.org/m2/"
			}
		}
		dependencies {
			classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE"
		}
	}

	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	publishing {
	    repositories {
	        maven {
	            url "s3://wes.maven.repo/libs-release/local"
	            credentials(AwsCredentials) {
	                accessKey "${System.getenv('AWS_KEY')}"
	                secretKey "${System.getenv('AWS_SECRET')}"
	            }
	        }
	    }

	    // to S3
	    publications {
	        mavenJava(MavenPublication) {
	            groupId project.group
	            artifactId project.name
	            version project.version
	            from components.java
	        }
	    }
	}

	task containerize(type: Exec) {
	    dependsOn build
	    executable "sh"
	    args "-c", "docker build -t ${project.group}/${project.name}:$version .".toLowerCase()
	}
}